* A modified and enhanced meta-oxide memristor model B1
*
.subckt B1 te be Y
*
*electrodes of the memristor element
*
.params gon = 0.01 goff = 0.00072 k = 3.2e5 mm = 1.12
.params m=1.13e-4 pp=12 uthr1= -0.15 uthr2= 0.15 x0=0.2
*
* parameters of the proposed memristor model
*
Cint Y 0 {1}

.IC V(Y)=x0
*
Rad Y 0 10G
*
*additional resistor for avoiding convergence problems
*
Gy 0 Y value={(k*(V(te,be)*(gon*V(Y)+goff*(1-V(Y))))*(1/(mm+1))*(mm*pow((sin(pi*V(Y))),2)+(1-pow((V(Y)-stpp(V(Y),m)),(2*pp)))))*(((stpp((uthr1-V(te,be)),m))+(stpp((V(te,be)-uthr2),m))))}
*
* expression for the memristor state variable 
*
G1 te be value={V(te,be)*(gon*V(Y)+goff*(1-V(Y)))}
*
*state-dependent current-voltage relation
*
.func stpp(x,p)={0.5*(1+(x/sqrt(pow(x,2)+p)))}
*
*a step-like flat function
*
.ends B1
*
* Sources:
*
*Mladenov, V., I. Zaykov, S. Kirilov, Application of a Nonlinear Drift Memristor Model in Analogue Reconfigurable Devices, 
*Int. Conf. Electronics 2022, doi: 10.1109/IEEECONF55059.2022.9810389.
*
*2. Analog Devices Corporation. LTspice [Online]. Available at: https://www.analog.com/en/index.html#LTspice
