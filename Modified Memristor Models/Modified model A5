*LTSpice modified nonlinear drift Lehtonen-Laiho library model of a titanium dioxide 
*memristor with a modified Joglekar-Biolek window and a voltage-dependent exponent 
*connections: te - top electrode, be - bottom electrode, Y - additional
*electrode for measuring the state variable
*
.subckt A5 te be Y
*
*memristor model parameters
.params a=1.1 m=5 mm=1e-4 b=10.37 cc=3.78 hi=20.1e-6 beta=60.3e-6 gama=1.31 alfa=1.42 n=5 x0=0.3
*
*capacitor for integration of memristor state variable time derivative
Cint Y 0 {1}
.IC V(Y)=x0
*
*additional parallel high-valued resistor for preventing convergence problems
Rad Y 0 10G
*
*memristor state variable
Gy 0 Y value={(a)*(pow(V(te,be),m))*(((1-pow((V(Y)-stpp(-V(te,be),mm)),(2*round(b/(cc+abs(V(te,be)))))))+((1-pow((2*V(Y)-1),(2*round(b/(cc+abs(V(te,be)))))))))/2)}
*
*memristor conductance
G1 te be value={(hi*(exp(gama*(V(te,be)))-1)+(beta*(pow(V(Y),n))*sinh(alfa*(V(te,be)))))}
*
* a step-like logistic function
.func stpp(x,p)={0.5*(1+(x/sqrt(pow(x,2)+p)))}
*
.ends A5
*
*properties: low, middle and high operating frequencies, low, middle and high signal levels, high nonlinearity,
*high accuracy, activation thresholds - applied, soft-switching and hard-switching 
*operating mode, partial representation of boundary effects, nonlinear ion drift - voltage 
*representation - applied, tunability - good, middle complexity, application - analysis of analog
* and digital devices, neural networks, memories
*
*Sources: 
*1. V. Mladenov, Analysis and Simulations of Hybrid Memory Scheme
*Based on Memristors, Electronics 2018, 7, 289; 
*doi:10.3390/electronics7110289, httPS://www.mdpi.com/2079-9292/7/11/289.
*
*2. Valeri Mladenov, Advanced Memristor Modeling - Memristor Circuits and Networks, 
*MDPI, ISBN 978-3-03897-103-0 (PDF), 2019, pp. 172.
*
*3. Analog Devices Corporation. LTspice [Online]. Available at: https://www.analog.com/en/index.html#LTspice
