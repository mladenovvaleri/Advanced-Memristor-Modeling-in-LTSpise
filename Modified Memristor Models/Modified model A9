*A modified memristor model for tantalum oxide memristors
*with simplified mathematical expressions
*memristor terminals - top electrode te, bottom electrode be, and
*additional electrode Y for measuring the state variable
*
.subckt A9 te be Y
*
*memristor model parameters
.params yon=0.06 sigmap=4e-5 Cint=1 A=1e-10 sigmaoff=0.013 sigmaon=4.5e-1
.params yoff=0.4 Gm=0.025 mm=0.3 beta=500 k=1000 h=0.003 B=1e-4 pp=10 x0=0.3
*
*modeling the memristor current
G1 te be value={V(te,be)*(Gm*V(Y)+(1-V(Y))*(h*(pow(V(te,be),2))))}
*
*modeling the memristor state
Gy 0 Y value={((A*sinh(V(te,be)/sigmaoff)*exp(-(yoff^2)/(V(Y)^2))*exp(1/(1+beta*I(G1)*V(te,be)))*(sttp(V(te,be),(-k))))+(B*sinh(V(te,be)/sigmaon)*exp(-(V(Y)^2)/(yon^2))*exp((V(te,be)*I(G1))/sigmap)*(sttp(V(te,be),k))))*((1/(mm+1))*(mm*pow((sin(pi*V(Y))),2)+(1-pow((V(Y)-sttp(V(te,be),k)),(2*pp)))))}
*
*capacitor for integrating and deriving the state variable
Cint Y 0 {1}
.IC V(Y)=x0
*
*additional resistor for avoiding convergence problems
Rad Y 0 1G
*
*step-like logistic function
.func sttp(x,p)={(tanh(x*p)+1)/2}
*
.ends A9
*
*properties: low, middle operating frequencies, low, middle signal levels, high nonlinearity,
*high accuracy, activation thresholds - not applied, soft-switching and hard-switching
*operating mode, representation of boundary effects - partial, nonlinear ion drift - voltage 
*representation - applied, tunability - good, middle complexity, application - analysis of analog
* and digital devices, neural networks, memories
*
*Sources: 
*1. Mladenov, V, “A Modified Tantalum Oxide Memristor Model 
*for Neural Networks with Memristor-Based Synapses,” IEEE 
*Conf. Pr. MOCAST 2020, DOI:10.1109/MOCAST49295.2020.9200238, pp.1-4.
*
*2. Analog Devices Corporation. LTspice [Online]. Available at: https://www.analog.com/en/index.html#LTspice
