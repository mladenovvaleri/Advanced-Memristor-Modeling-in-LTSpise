*A modified model for metal oxide memristors
*with simplified mathematical expressions
*memristor terminals - top electrode te, bottom electrode be, and
*additional electrode Y for measuring the state variable
*
.subckt C5 te be Y
*
.params k1=0.11 k2=0.13 k3=803.2 k4=1.52
.params m=1.3e-7 vthr=0.1 pp=10 x0=0.3
*
Cint Y 0 {1}
.IC V(Y)=x0
*
Rad Y 0 100G
*
Gy 0 Y value={k3*pow((k4*V(te,be)),3)*(1-pow((V(Y)-stpp(-V(te,be),m)),(2*pp)))*(stpp((abs(V(te,be))-vthr),m))}
*
G1 te be value={(k1*pow(V(te,be),5)+k2*V(te,be))*pow(V(Y),5)}
*
.func stpp(x,p)={0.5*(1+(x/sqrt(pow(x,2)+p)))}
*
.ends C5
*
*properties: middle, high operating frequencies, low, middle, high signal levels, high nonlinearity,
*good accuracy, activation thresholds, soft-switching and hard-switching
*modes, representation of the boundary effects, nonlinear ion drift - voltage
*relationship, tunability, low complexity, application - analysis of
*analog reconfigurable devices - filters, oscillators, memristor memories
*
*Sources:
*1. Kirilov, S., I. Zaykov, “A metal oxide memristor-based oscillators and filters”, Proceedings
*of Technical University of Sofia, ISSN: 2738-8549, VOL. 72, NO. 2,
*https://doi.org/10.47978/TUS.2022.72.02.006, pp. 32 – 37.
*
*2. Analog Devices Corporation. LTspice [Online]. Available at: https://www.analog.com/en/index.html#LTspice
