*LTSPICE library model of titanium dioxide memristor with Strukov-Williams's window function
*fsw=x*(1-x), where x is the memristor state variable
*representing the nonlinear ionic dopant drift for high-level signals
*terminals: top electrode (te), bottom electrode (be) and electrode for
*measuring the state variable (Y)
*The model is appropriate for low-frequency and high-amplitude signals
*for analysis of titanium dioxide based memristors and memristor circuits
.subckt K1 te be Y
*memristor parameters
*ron - resistance of the memristor in low-resistance state
*roff - resistance of the memristor for high-resistance state
*k - a constant dependent on the ionic mobility mu, memristor length D and
*the low-resistance ron: k=(mu*ron)/(D^2);
.params ron=100 roff=16e3 k=10e3 Cint=1
*capacitor for integration the state variable time derivative
Cint Y 0 1 IC=0.3
*additional high-valued resistance for preventing convergence problems
Rad Y 0 1G
*memristor state modeling
Gy 0 Y value={(k*V(te,be)*(1/(ron*(V(Y))+roff*(1-V(Y))))*(4*V(Y)*(1-V(Y))))}
*memristor conductance
G1 te be value={V(te,be)*((1/(ron*(V(Y))+roff*(1-V(Y)))))}
.ends K1
*properties: low operating frequencies, low and middle signal levels, middle nonlinearity,
*low accuracy, activation thresholds - not used, soft-switching operating mode, partial
*representation of boundary effects, nonlinear ion drift - voltage representation - not used,
*tunability - no, low complexity, application - analysis of analog and digital devices
*
*Sources: 1. Strukov, D.; Snider, G.; Stewart, D.; Williams, R.S. The Missing 
Memristor Found. Nat. Lett. 2008, 453, doi:10.1038/nature06932, pp. 80–83.
* 2. Joglekar, Y.; Wolf, S. The Elusive Memristor: Properties of Basic 
*Electrical Circuits. Eur. J. Phys. 2009, Vol. 30, pp. 661–675.
*3. Biolek, Z.; Biolek, D.; Biolkova, V. SPICE Model of Memristor with Nonlinear
*Dopant Drift. Radioengineering 2009, 18, pp. 210–214.
