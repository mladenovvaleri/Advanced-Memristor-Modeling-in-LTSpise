*LTSPICE library model of titanium dioxide memristor with Ascoli-Corinto window function
*fAC=1, if 0<x<1, fAC=1, if x=0 and v>=0, fAC=1 if x=1 and v<=0,
*fAC=0 if x=0 and v<0, fAC=0 if x=1 and v>0
*this window function represents correctly the boundary effects
*memristor terminals: top electrode te, bottom electrode be, and additional electrode Y
*for measuring the memristor state variable x
.subckt K4 te be Y
*memristor parameters
*ron - low resistance state, roff - high resistance state, k - a constant
*dependent on ron, the ionic mobility mu and the memristor length D
*pp - positive integer exponent with a very high value for
*approximation the window function, in this case its behaviour is near
*to the idealized Ascoli-Corinto window function
*a step-like logistic function stpp(i,m)=0.5*(1+(i/sqrt(pow(x,2)+m)))
*applied for avoiding convergence problems
.params ron=100 roff=16e3 k=10e3 Cint=1 pp=200 m=1e-4
*capacitor for integration the state variable time derivative
Cint Y 0 1 IC=0.3
*high-valued resistance for preventing convergence problems
Rad Y 0 1G
*memristor state modeling
Gy 0 Y value={(k*V(te,be)*(1/(ron*(V(Y))+roff*(1-V(Y))))*(1-pow((V(Y)-stpp(-V(te,be),m)),(2*pp))))}   
*memristor conductance
G1 te be value={V(te,be)*((1/(ron*(V(Y))+roff*(1-V(Y)))))}
* step-like logistic window function
.func stpp(x,p)={0.5*(1+(x/sqrt(pow(x,2)+p)))}
.ends K4
*properties: low operating frequencies, low and middle signal levels, middle nonlinearity,
*satisfactory accuracy, activation thresholds - applied, soft-switching and hard-switching 
*operating mode, representation of boundary effects, nonlinear ion drift - voltage 
*representation - not used, tunability - partial, middle complexity, application - analysis of analog
* and digital devices, neural networks
*
*Sources: 1. Corinto, F.; Ascoli, A. A Boundary Condition-Based 
*Approach to the Modeling of Memristor Nanostructures. IEEE Trans. 
*Circuits Syst. 2012, DOI: 10.1109/TCSI.2012.2190563, Vol. 59, pp. 2713–2726.
* 2. Ascoli, A.; Tetzlaff, R.; Corinto, F.; Gilli, M. PSpice switch-based versatile memristor 
* model. IEEE Int. Symp. Circuits Syst. 2013, DOI: 10.1109/ISCAS.2013.6571818, pp. 205–208.
