* Memristor model for tantalum oxide material by Hewlett-Packard
*modified by Ascoli and Tetzlaff
*memristor terminals - top electrode te, bottom electrode be, and
*additional electrode Y for measuring the state variable
.subckt K9 te be Y
*memristor parameters
.params yon=0.06 sigmap=4e-5 Cint=1 A=1e-10 sigmaoff=0.013 B=1e-4 ro=100
.params yoff=0.4 Gm=0.025 m=1e-4 beta=500 aa=3.5e-6 bb=3.1 sigmaon=0.45
* modeling the memristor current
G1 te be value={V(te,be)*(Gm*V(Y)+(1-V(Y))*(aa*exp(bb*sqrt(V(te,be)*((1/(1+exp(-ro*V(te,be))))-(1/(1+exp(ro*V(te,be)))))))))}
*modeling the memristor state
Gy 0 Y value={((A*sinh(V(te,be)/sigmaoff)*exp(-pow(yoff/V(Y),2))*exp(1/(1+beta*I(G1)*V(te,be)))*stpp(-V(te,be),m))+(((B*sinh(V(te,be)/sigmaon))*exp(-pow(V(Y)/yon,2))*exp(I(G1)*V(te,be)/sigmap)*stpp(V(te,be),m))))}
* capacitor modeling the state
Cint Y 0 1 IC=0.3
* resistor for prevention of convergence problems
Rad Y 0 10MEG
* Step-like smooth function
.func stpp(x,p)={0.5*(1+(x/sqrt(pow(x,2)+p)))}
.ends K9
*properties: low, middle and high operating frequencies, low, middle signal levels, high nonlinearity,
*high accuracy, activation thresholds - not applied, soft-switching and hard-switching
*operating mode, representation of boundary effects - partial, nonlinear ion drift - voltage 
*representation - applied, tunability - good, middle complexity, application - analysis of analog
* and digital devices, neural networks, memories
*
*Sources: 1. A. Ascoli, R. Tetzlaff, L. Chua, “Robust Simulation 
*of a TaO Memristor Model,” Radioengineering, Vol. 24, No. 2, 
*June 2015, DOI: 10.13164/re.2015.0384, pp. 384 – 392.
*2. J. Strachan, A. Torrezan, F. Miao, M. Pickett, J. Yang, 
*W. Yi, G. Medeiros-Ribeiro, R. S. Williams, “State Dynamics and Modeling 
*of Tantalum Oxide Memristors,” IEEE Transactions on Electron 
*Devices, Vol. 60, No. 7, July 2013, pp. 2194 - 2202.
*3. Analog Devices Corporation. LTspice [Online]. Available at: https://www.analog.com/en/index.html#LTspice
